
def snapshot = true

group = 'no.mechatronics.sfi.fmi4j'
version = '0.2.8'
if (snapshot) {
    version += '-SNAPSHOT'
}

def fmi_md_version = snapshot ? '0.5.8-SNAPSHOT' : '0.5.7'

apply plugin: 'maven'
apply plugin: 'signing'

if (snapshot) {
    repositories {
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {

    api group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'

    implementation group: 'net.java.dev.jna', name: 'jna', version: '4.5.1'
    implementation group: 'net.java.dev.jna', name: 'jna-platform', version: '4.5.1'

    if (true) {
        api group: 'no.mechatronics.sfi.fmi4j', name: 'fmi-md', version: fmi_md_version, changing: snapshot
    } else {
        compile project(':fmi-md')
    }
   // testImplementation group: 'org.siani.javafmi', name: 'fmu-wrapper', version: '2.24.1'

}

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name archivesBaseName
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A Java/Kotlin library for dealing with Functional Mock-up Units (FMUs) on the JVM platform'
                url 'https://github.com/SFI-Mechatronics/fmi4j'

                scm {
                    connection 'scm:git:git://github.com/SFI-Mechatronics/fmi4j.git'
                    developerConnection 'scm:git:ssh://github.com/SFI-Mechatronics/fmi4j.git'
                    url 'https://github.com/SFI-Mechatronics/fmi4j/tree/master'
                }

                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'laht'
                        name 'Lars Ivar Hatledal'
                        email 'laht@ntnu.no'
                    }
                }
            }
        }
    }
}

//include if users want to generate a fatJar from source
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': "1.0",
                'Main-Class': ''
    }
    baseName = project.name
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}



