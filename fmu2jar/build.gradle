
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

def group = "no.mechatronics.sfi.fmu2jar"
def version = "1.0-SNAPSHOT"
def fmuName = "ControlledTemperature"
def m2 = System.getProperty("user.home") + "/.m2/repository"
def jar = new File("${m2}/${group.replace(".", "/")}/${fmuName}/$version/${fmuName}-${version}.jar")

def debug = jar.exists()
println("${archivesBaseName}:debug mode=${debug}")

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = "${group}.Main"

repositories {
    if (debug) {
        mavenLocal()
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {

    implementation group: 'info.picocli', name: 'picocli', version: '3.1.0'

    compile project(':fmi-modeldescription')

    if (debug) {
        testCompile group: "$group", name: "$fmuName", version: "$version", changing: true
    } else {
        testCompile project(':fmi-import')
    }

}

shadowJar {
    baseName = 'fmu2jar'
    classifier = null
    version = null
}

sourceSets {
    test {
        java {
            if (!debug) {
                exclude '**/ControlledTemperatureTest.java'
            }
        }
        kotlin {
            if (!debug) {
                exclude '**/ControlledTemperatureTest_kt.kt'
            }
        }
    }
}

task deleteCachedControlledTemperature(type: Delete) {
    delete "${m2}/${group.replace(".", "/")}/${fmuName}"
}

