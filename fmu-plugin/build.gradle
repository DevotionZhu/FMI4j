plugins {
    id 'java'
    id 'kotlin'
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version "0.12.0"
}

apply from: rootProject.file("gradle/junit.gradle")

dependencies {
    compileOnly project(':fmu-builder')

    testImplementation gradleTestKit()
}

jar {
    from {
        def libs = ["FMI4j", "picocli", "javax"]
        configurations.compileClasspath.filter { file ->
            def flag = false
            libs.each { lib ->
                if (file.absolutePath.contains(lib)) {
                    flag = true
                }
            }
            flag
        } collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

gradlePlugin {
    plugins {
        fmuExportPlugin {
            id = 'no.ntnu.ihb.fmi4j.fmu-export'
            implementationClass = 'no.ntnu.ihb.fmi4j.FmuExportPlugin'
        }
    }
}

pluginBundle {
    website = "https://github.com/NTNU-IHB/FMI4j"
    vcsUrl = "https://github.com/NTNU-IHB/FMI4j"
    description = "A plugin for exporting FMUs from Java code"
    tags = ["gradle", "fmi4j"]
    plugins {
        fmuExportPlugin {
            displayName = "Gradle fmi4j-export plugin"
        }
    }
}
