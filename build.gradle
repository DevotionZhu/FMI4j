
plugins {
    id "base"
    id "org.jetbrains.kotlin.jvm" version "1.3.61" apply false
}

println "Gradle version is ${gradle.getGradleVersion()}"

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '6.0.1'
}

subprojects { sub ->

    group = 'no.ntnu.ihb.fmi4j'
    version = '0.27.1'

    pluginManager.withPlugin('java') {

        repositories {
            mavenCentral()
        }

        pluginManager.withPlugin('kotlin') {

            compileKotlin {
                kotlinOptions {
                    jvmTarget = "1.8"
                    javaParameters = true
                }
            }
            compileTestKotlin {
                kotlinOptions.jvmTarget = "1.8"
            }

        }

    }

}

tasks.create('cleanTemp') {

    doLast {
        def temp = new File(System.getProperty("java.io.tmpdir"))
        temp.listFiles(new FilenameFilter() {
            @Override
            boolean accept(File dir, String name) {
                return dir == temp && name.startsWith("fmi4j_")
            }
        }).each {
            it.deleteDir()
        }
    }

}

clean.dependsOn(cleanTemp)
