
plugins {
    id "base"
    id "org.jetbrains.kotlin.jvm" version "1.3.50" apply false
}

println "Gradle version is ${gradle.getGradleVersion()}"

group = 'no.ntnu.ihb.fmi4j'

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

subprojects { sub ->

    if (sub.name != "fmi-native") {

        apply plugin: 'java'
        apply plugin: 'kotlin'

        repositories {
            mavenCentral()
        }

        dependencies {

            implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

            def slf4j_version = '1.7.25'
            implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
            runtimeOnly group: 'org.slf4j', name: 'slf4j-log4j12', version: slf4j_version

        }

        compileKotlin {
            kotlinOptions.jvmTarget = "1.8"
        }
        compileTestKotlin {
            kotlinOptions.jvmTarget = "1.8"
        }
        
    }

}

tasks.create('cleanTemp') {

    doLast {
        def temp = new File(System.getProperty("java.io.tmpdir"))
        temp.listFiles(new FilenameFilter() {
            @Override
            boolean accept(File dir, String name) {
                return dir == temp && name.startsWith("fmi4j_")
            }
        }).each {
            it.deleteDir()
        }
    }

}

clean.dependsOn(cleanTemp)
