
plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.51" apply false //apply in sub-projects
}

println "Gradle version is ${GradleVersion.current().version}"

subprojects { sub ->

    apply plugin: 'kotlin'
    apply plugin: 'java-library'

    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    def slf4j_version = '1.7.25'
    def junit_version = '5.2.0'

    dependencies {

        sub.plugins.withId ('kotlin') {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        }

        implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
        runtimeOnly group: 'org.slf4j', name: 'slf4j-log4j12', version: slf4j_version

        testImplementation("org.junit.jupiter:junit-jupiter-api:$junit_version")
        testImplementation("org.junit.jupiter:junit-jupiter-params:$junit_version")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junit_version")

    }

    test {
        useJUnitPlatform()
    }

    sub.plugins.withId ('kotlin') {
        compileKotlin {
            kotlinOptions {
                jvmTarget = "1.8"
                freeCompilerArgs = ['-Xjvm-default=enable']
            }
        }
        compileTestKotlin {
            kotlinOptions.jvmTarget = "1.8"
        }
    }

}




