
plugins {
    id "java-library"
    id "maven-publish"
}

println "Gradle version is ${GradleVersion.current().version}"

version = '1.0-SNAPSHOT'
group = 'no.mechatronics.sfi.fmu2jar'

def fmi4j_snapshot = true
def fmi4j_version = !fmi4j_snapshot ? '0.11.1' : 'dev-SNAPSHOT'

repositories {
    mavenCentral()
    if (fmi4j_snapshot) {
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }
}

if (fmi4j_snapshot) {
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

dependencies {
    api group: 'no.mechatronics.sfi.fmi4j', name: 'fmi-import', version: fmi4j_version, changing: fmi4j_snapshot
    api group: 'no.mechatronics.sfi.fmi4j', name: 'fmi-import-solvers-apache-math3', version: fmi4j_version, changing: fmi4j_snapshot
}

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jar {
    manifest {
        attributes("Class-Path": configurations.compile
                .collect { it.getName() }.join(' '))
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }

}