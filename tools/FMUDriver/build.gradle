
plugins {
    id 'application'
    id "org.jetbrains.kotlin.jvm" version "1.3.10"
    id "com.github.johnrengelman.shadow" version "4.0.2"
}

println "Gradle version is ${gradle.getGradleVersion()}"

def group = "no.mechatronics.sfi.fmi4j.crosscheck"
mainClassName = "${group}.FmuDriver"

def fmi4j_snapshot = false
def fmi4j_version = fmi4j_snapshot ? 'dev-SNAPSHOT' : '0.13.1'

repositories {
    mavenCentral()
    if (fmi4j_snapshot) {
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
}

if (fmi4j_snapshot) {
    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

dependencies {

    compile group: 'info.picocli', name: 'picocli', version: '3.1.0'
    compile group: 'org.apache.commons', name: 'commons-csv', version: '1.5'

    compile group: 'no.mechatronics.sfi.fmi4j', name: 'fmi-import', version: fmi4j_version, changing: fmi4j_snapshot
    compile group: 'no.mechatronics.sfi.fmi4j', name: 'fmi-import-solvers-apache-math3', version: fmi4j_version, changing: fmi4j_snapshot

    def slf4j_version = '1.7.25'
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
    runtime group: 'org.slf4j', name: 'slf4j-log4j12', version: slf4j_version

    def junit_version = '5.3.1'
    testCompile("org.junit.jupiter:junit-jupiter-api:$junit_version")
    testCompile("org.junit.jupiter:junit-jupiter-params:$junit_version")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junit_version")

}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    baseName = "fmudriver"
    classifier = null
    version = null
}

distributions {
    main {
        baseName = 'fmudriver'
    }
}

startScripts {
    applicationName = 'fmudriver'
}