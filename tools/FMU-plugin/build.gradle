
plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.51" apply false
}


ext.snapshot = true
ext.plugin_version = '0.1'
if (snapshot) {
    plugin_version += "-SNAPSHOT"
}
ext.plugin_group = "no.mechatronics.sfi.fmi4j"
ext.pluginId = "${plugin_group}.FmuPlugin"
ext.fmi4j_version = !snapshot ? '0.8' : '0.8.1-SNAPSHOT'

subprojects { sub ->

    apply plugin: 'kotlin'

    repositories {
        jcenter()
        if (snapshot) {
            maven {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            }
        }
    }

    def slf4j_version = '1.7.25'
    def junit_version = '5.2.0'

    if (snapshot) {
       configurations.all {
           resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
       }
    }

    dependencies {

        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

        implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
        runtimeOnly group: 'org.slf4j', name: 'slf4j-log4j12', version: slf4j_version

        testImplementation gradleTestKit()
        testImplementation("org.junit.jupiter:junit-jupiter-api:$junit_version")
        testImplementation("org.junit.jupiter:junit-jupiter-params:$junit_version")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junit_version")

    }

    test {
        useJUnitPlatform()
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

}
