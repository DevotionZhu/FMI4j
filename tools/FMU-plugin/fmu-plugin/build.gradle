
plugins {
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.0'
    id "org.gradle.kotlin.kotlin-dsl" version '0.18.0'
}

version = "0.1-SNAPSHOT"
group = "no.mechatronics.sfi.fmi4j"
println("maven version for ${archivesBaseName} is: ${version}")

def pluginId = "${group}.${archivesBaseName}"
def description = "Gradle plugin for simplifying programmatic interaction with FMUs"

dependencies {
    implementation group: "no.mechatronics.sfi.fmi4j", name: 'fmi-modeldescription', version: '0.8'
}

gradlePlugin {
    plugins {
        fmuPlugin {
            id = pluginId
            implementationClass = "${group}.FmuPlugin"
        }
    }
}

pluginBundle {
    website = 'https://sfi-mechatronics.github.io/FMI4j/'
    vcsUrl = 'https://github.com/SFI-Mechatronics/FMI4j'
    description = description
    tags = ['fmi', 'fmu', 'fmi4j']

    plugins {
        fmuPlugin {
            displayName = "Gradle FMU plugin"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

